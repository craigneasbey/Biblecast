apply plugin: 'com.android.application'

android {
    signingConfigs {
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "au.id.neasbey.biblecast"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 2
        versionName "1.1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            // https://code.google.com/p/android/issues/detail?id=123771
            //testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debuggable.initWith(buildTypes.debug)
        debuggable {
            testCoverageEnabled = false
        }
    }
    testOptions {
        unitTests.all {
            systemProperty 'robolectric.logging', 'stdout'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
    applicationVariants.all { variant ->
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            source = variant.javaCompile.source
            classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
            options {
                links "http://docs.oracle.com/javase/7/docs/api/"
                linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
            }
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:mediarouter-v7:23.1.1'
    compile 'com.google.android.gms:play-services-cast:7.8.0'

    androidTestCompile 'com.android.support:appcompat-v7:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    // Set this dependency to build and run Espresso tests
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.1'
    // Espresso-idling-resource for synchronization with background jobs
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.1'

    // Set this dependency to build and run UI Automator tests
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'

    // the following is required to use Mockito and JUnit for your
    // instrumentation unit tests on the JVM
    androidTestCompile 'junit:junit:4.12'

    // the following is required to use Mockito and JUnit for your unit
    // tests on the JVM
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.1'
    testCompile "org.robolectric:robolectric:3.0"
}
